#!/usr/bin/sed -f

#==============================================================================
# sed-script para converter numeros arabicos em algarismos romanos
#==============================================================================

b main;

: milhar;
s/1\(...\)/M\1/;
s/2\(...\)/MM\1/;
s/3\(...\)/MMM\1/;

: centena;
s/1\(..\)/C\1/;
s/2\(..\)/CC\1/;
s/3\(..\)/CCC\1/;
s/4\(..\)/CD\1/;
s/5\(..\)/D\1/;
s/6\(..\)/DC\1/;
s/7\(..\)/DCC\1/;
s/8\(..\)/DCCC\1/;
s/9\(..\)/CM\1/;

: dezena;
s/1\(.\)/X\1/;
s/2\(.\)/XX\1/;
s/3\(.\)/XXX\1/;
s/4\(.\)/XL\1/;
s/5\(.\)/L\1/;
s/6\(.\)/LX\1/;
s/7\(.\)/LXX\1/;
s/8\(.\)/LXXX\1/;
s/9\(.\)/XC\1/;

: unidade;
s/1/I/;
s/2/II/;
s/3/III/;
s/4/IV/;
s/5/V/;
s/6/VI/;
s/7/VII/;
s/8/VIII/;
s/9/IX/;

s/0\+$//;
s/\(.\)0\+\(.\)/\1\2/;
n;

: main;
/^\([1-9]\|[1-9][0-9]\|[1-9][0-9][0-9]\|[1-3][0-9]\{3\}\)$/ !d;
/^.$/    b unidade;
/^..$/   b dezena;
/^...$/  b centena;
/^....$/ b milhar;
